"""
Author: Miquel Anglada Girotto
Contact: miquelangladagirotto [at] gmail [dot] com
Last Update: 2021-01-12

Workflow purpose
--------------
Workflow to analyze putative associations between exons and cancer at single-exon level.


Outline
-------
        
"""

import os

# variables
ROOT = os.path.dirname(os.path.dirname(os.getcwd()))
RAW_DIR = os.path.join(ROOT,'data','raw')
PREP_DIR = os.path.join(ROOT,'data','prep')
RESULTS_DIR = os.path.join(ROOT,'results','model_splicing_dependency')

EVENT_TYPES = ['EX','ALTA','ALTD']

##### RULES #####
rule all:
    input:
        # fit gene dependencies
        expand(os.path.join(RESULTS_DIR,'files','models_gene_dependency-{event_type}.tsv.gz'), event_type=EVENT_TYPES),
        
        # compute splicing dependencies
        expand(os.path.join(RESULTS_DIR,'files','splicing_dependency-{event_type}.tsv.gz'), event_type=EVENT_TYPES)
        
        # EDA splicing dependencies
        # validation splicing dependencies
        
        
rule model_gene_dependency:
    input:
        psi_file = os.path.join(PREP_DIR,'exon_psi','CCLE-{event_type}.tsv.gz'),
        genexpr_file = os.path.join(PREP_DIR,'genexpr','CCLE.tsv.gz'),
        rnai_file = os.path.join(PREP_DIR,'demeter2','CCLE.tsv.gz'),
        annotation_file = os.path.join(RAW_DIR,'VastDB','EVENT_INFO-hg38_noseqs.tsv'),
        metadata_file = os.path.join(PREP_DIR,'metadata','CCLE.tsv') 
    output:
        os.path.join(RESULTS_DIR,'files','models_gene_dependency-{event_type}.tsv.gz')
    threads: 15
    shell:
        """
        python scripts/model_gene_dependency.py \
                    --psi_file={input.psi_file} \
                    --genexpr_file={input.genexpr_file} \
                    --annotation_file={input.annotation_file} \
                    --rnai_file={input.rnai_file} \
                    --metadata_file={input.metadata_file} \
                    --output_file={output} \
                    --n_jobs={threads}
        """
        
        
rule compute_splicing_dependency:
    input:
        psi = os.path.join(PREP_DIR,'event_psi','CCLE-{event_type}.tsv.gz'),
        genexpr = os.path.join(PREP_DIR,'genexpr_tpm','CCLE.tsv.gz'),
        models = os.path.join(RESULTS_DIR,'files','models_gene_dependency-{event_type}.tsv.gz')
    output:
        os.path.join(RESULTS_DIR,'files','splicing_dependency-{event_type}.tsv.gz')
    shell:
        """
        python scripts/compute_splicing_dependency.py \
                    --psi_file={input.psi} \
                    --genexpr_file={input.genexpr} \
                    --models_file={input.models} \
                    --output_file={output}
        """
    
    
rule figures_eda:
    input:
        psi_file = os.path.join(PREP_DIR,'exon_psi','CCLE.tsv.gz'),
        genexpr_file = os.path.join(RAW_DIR,'DepMap','achilles_ccle','CCLE_expression_transposed.tsv.gz'),
        rnai_file =  os.path.join(RAW_DIR,'DepMap','demeter2','D2_combined_gene_dep_scores.csv'),
        annotation_file = os.path.join(RAW_DIR,'VastDB','EVENT_INFO-hg38_noseqs.tsv'),
        metadata_file = os.path.join(PREP_DIR,'metadata','CCLE.tsv'),
        dependencies_file = os.path.join(RESULTS_DIR,'files','models_gene_dependency.tsv.gz')
    output:
        directory(os.path.join(RESULTS_DIR,'figures','eda'))
    shell:
        """
        Rscript scripts/figures_eda.R
        """
    
    
rule figures_validation:
    input:
        crispr_screen_file = os.path.join(PREP_DIR,'exon_psi','CCLE.tsv.gz'),
    output:
        directory(os.path.join(RESULTS_DIR,'figures','validation'))
    shell:
        """
        Rscript scripts/figures_validation.R
        """
    